if (rootProject.ext.isAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
//    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {

        if (rootProject.ext.isAlone) {
            applicationId "com.example.cootek.newfastframe"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (!rootProject.ext.isAlone) {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [moduleName: project.getName()]
                }
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main {
            if (rootProject.ext.isAlone) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/module/java']
                res.srcDirs = ['src/main/res', 'src/main/module/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
            jniLibs.srcDirs = ['libs']

        }
    }
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation project(':commonlibrary')
    //用作颜色选择器
    implementation ('com.afollestad.material-dialogs:commons:0.9.1.0'){
        exclude module: "support-v4"
        exclude module:"support-core-ui"
        exclude module:"support-compat"
        exclude module:"appcompat-v7"
        exclude module :"recyclerview-v7"
    }
    implementation ('com.komi.slider:slider:0.4.0'){
        exclude module: "support-v4"
        exclude module:"support-core-ui"
        exclude module:"support-compat"
        exclude module:"appcompat-v7"
        exclude module :"recyclerview-v7"
    }
    annotationProcessor rootProject.ext.dependencies.dagger2_compiler
    if (!rootProject.ext.isAlone) {
        annotationProcessor rootProject.ext.dependencies.arouter_compiler
    }
    testImplementation 'junit:junit:4.12'
//    compile files('libs/nineoldandroids-2.4.0.jar')
}

